apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

def loadSecretProperties = { buildType ->
    Properties props = new Properties()
    File propsFile = new File("secret.${buildType}.properties")
    if(propsFile.exists()) props.load(propsFile.newDataInputStream())
    return props;
}

android {
    compileSdkVersion 21
    buildToolsVersion '25.0.0'

    defaultConfig {
        applicationId "com.infora.ledger"
        minSdkVersion 19
        targetSdkVersion 21
        versionCode 24 //Should be bumped after each release
        versionName "1.0-a"
    }
    signingConfigs {
        release {
            def props = loadSecretProperties('release')
            storeFile file(props.getProperty("keystore-path", "null"))
            storePassword props.getProperty("keystore-password", "null")
            keyAlias props.getProperty("keystore-key-alias", "null")
            keyPassword props.getProperty("keystore-key-password", "null")
        }
    }
    buildTypes {
        debug {
            def props = loadSecretProperties('debug')
            buildConfigField "String", "GOAUTH_API_CLIENT_ID", "\"${props.getProperty('goauth-api-client-id')}\""
            buildConfigField "String", "DEFAULT_LEDGER_HOST", "\"${props.getProperty('ledger.default-host')}\""
        }
        release {
            def props = loadSecretProperties('release')
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "GOAUTH_API_CLIENT_ID", "\"${props.getProperty('goauth-api-client-id')}\""
            buildConfigField "String", "DEFAULT_LEDGER_HOST", "\"${props.getProperty('ledger.default-host')}\""
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'de.greenrobot:eventbus:2.4.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile 'com.google.android.gms:play-services:6.5.87'
    compile 'commons-codec:commons-codec:1.9'
    compile 'com.j256.ormlite:ormlite-android:4.48'
    compile 'org.jsoup:jsoup:1.8.2'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.google.dagger:dagger:2.0.1'
    apt 'com.google.dagger:dagger-compiler:2.0.1'
    androidTestApt 'com.google.dagger:dagger-compiler:2.0.1'
    provided 'org.glassfish:javax.annotation:10.0-b28'
}