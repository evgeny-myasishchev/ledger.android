apply plugin: 'com.android.application'

def getGoauthApiClientId = { file ->
    Properties props = new Properties()
    File propsFile = new File(file)
    if(propsFile.exists()) props.load(propsFile.newDataInputStream())
    return props.getProperty('goauth-api-client-id')
}

def loadSecretProperties = { file ->
    Properties props = new Properties()
    File propsFile = new File(file)
    if(propsFile.exists()) props.load(propsFile.newDataInputStream())
    return props;
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "com.infora.ledger"
        minSdkVersion 19
        targetSdkVersion 21
        versionCode 1
        versionName "1.0-a"
    }
    signingConfigs {
        release {
            def props = loadSecretProperties('secret.release.properties')
            storeFile file(props.getProperty("keystore-path"))
            storePassword props.getProperty("keystore-password")
            keyAlias props.getProperty("keystore-key-alias")
            keyPassword props.getProperty("keystore-key-password")
        }
    }
    buildTypes {
        debug {
            buildConfigField "String", "GOAUTH_API_CLIENT_ID", "\"" + getGoauthApiClientId('secret.debug.properties') + "\""
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "GOAUTH_API_CLIENT_ID", "\"" + getGoauthApiClientId('secret.release.properties') + "\""
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'de.greenrobot:eventbus:2.4.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp:2.2.0'
    compile 'com.google.android.gms:play-services:6.5.87'
}